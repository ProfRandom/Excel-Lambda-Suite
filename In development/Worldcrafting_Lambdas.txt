CHUHALA_DAY_TO_DATE = LAMBDA(input, [mode],

    LET(
        mode, if(mode>2, 0, mode),
        dom, mod(round(input,1),30),
        dos, mod(input, 90),
        day, if(dom=0, 30, dom),
        mth, rounddown(input/30,0)+1,
        mon, if(day=30, mth-1, mth),
        finday, round(day,0),
        date, textjoin("/",,mon,day),
        ssn, roundup(mon/3,0),
        season, SWITCH(ssn,
            1, "Spring",
            2, "Summer",
            3, "Autumn",
            4, "Winter",
            5, "YearEnd"),
        pfx, mod(mon,3),
        pref, SWITCH(pfx,
            0, "Aft-",
            1, "Fore-",
            2, "Full-"),
        prefix, if(ssn=5, "", pref),
        month, textjoin("",,prefix,season),
        fulldate, textjoin(" ",,finday, month),
        datefull, textjoin(" ",, month, finday),
        output, SWITCH(mode,
            1, date,
            0, fulldate,
            2, datefull),
        output
    )
);