ENGNOTE = LAMBDA(input, [prec],
    LET(
        punct, IFERROR(SEARCH(".", input), 0),
        loc, IF(punct = 0, 0, LEN(input) - punct),
        prec, IF(ISOMITTED(prec), 2, prec),
        power, TRUNC(LOG10(input)),
        divisor, MAX(0, power - MOD(power, 3)),
        adjust, 10 ^ divisor,
        value, ROUND(input / adjust, prec),
        suff, "E+" & TEXT(divisor, REPT("0", 2)),
        /*FIXED(value, IF(punct = 0, LEN(input), loc), TRUE) & suff*/
        output, TEXTJOIN("", , FIXED(value, prec), suff),
        output
    )
);



NUMMAG = LAMBDA(input, [decprec],
    LET(
        max, 30,
        decprec, IF(ISOMITTED(decprec), 3, decprec),
        input, ABS(input),
        power, TRUNC(LOG(input)),
        category, MOD(power, 3),
        divisor, SWITCH(category, 0, power, 1, power - 1, 2, power - 2),
        adjust, IFS(divisor <= max, 10 ^ divisor, divisor > max, 10 ^ max),
        value, input / adjust,
        suff, SWITCH(
            divisor,
            0, "â€”",
            3, "k",
            6, "M",
            9, "G",
            12, "T",
            15, "P",
            18, "E",
            21, "Z",
            24, "Y",
            27, "R",
            "Q"
        ),
        FIXED(value, decprec, TRUE) & " " & suff
        /*divisor & " " & adjust & " " & value & " " & suff*/
        
    )
);

NUMPOWR = LAMBDA(input, [mode],
    LET(
        mode, IF((mode > 2) + (ISOMITTED(mode)), 0, mode),
        mag, TRUNC(LOG(input)),
        power, SWITCH(
            mode,
            0, TEXTJOIN("", , "E+", TEXT(mag, "00")),
            1, TEXTJOIN("", , "10^", mag),
            2, TEXT(POWER(10, TRUNC(LOG(input))), "0E+00"),
            
        ),
        power
    )
);

NUM_ONLY = LAMBDA(input,
    LET(
        get_codes, CODE(MID(LOWER(input), SEQUENCE(LEN(input)), 1)),
        make_num, SUM(--BYROW(get_codes, LAMBDA(z, z = 46))),
        output, IF(
            make_num >= 2,
            CONCAT(BYROW(get_codes, LAMBDA(x, IF(AND(OR(x < 48, x > 57), x <> 46), "", CHAR(x))))),
            IFERROR(
                VALUE(CONCAT(BYROW(get_codes, LAMBDA(x, IF(AND(OR(x < 48, x > 57), x <> 46), "", CHAR(x)))))),
                "#NONE!"
            )
        ),
        output
    )
);

